buildscript {

    ext {
        // Airship Version - major.minor.patch
        airshipVersion = '14.1.1'

        // Airship Version Qualifier beta, release, etc...
        // airshipVersionQualifier = "beta"

        // Android SDK Versions
        compileSdkVersion = 29
        targetSdkVersion = 29
        minSdkVersion = 16

        // Gradle plugins
        gradlePluginVersion = '4.1.1'
        bintrayPluginVersion = '1.8.4'
        googleServicesPluginVersion = '4.3.3'
        spotlessPluginVersion = '3.27.1'
        ktlintPluginVersion = '0.36.0'

        // Dependencies

        // Kotlin
        kotlinVersion = '1.3.72'
        kotlinCoroutinesVersion = '1.3.4'

        // Androidx
        androidxCoreVersion = '1.3.2'
        androidxFragmentVersion = '1.2.5'
        androidxAnnotationVersion = '1.1.0'
        androidxCustomViewVersion = '1.1.0'
        androidxAppCompatVersion = '1.2.0'
        androidxPreferenceVersion = '1.1.1'
        androidxVectorDrawableVersion = '1.1.0'
        androidxRoomVersion = '2.2.5'
        androidxConstraintLayoutVersion = '2.0.4'
        androidxPagingVersion = '2.1.2'
        androidxLifeCycleVersion = '2.2.0'

        androidxNavigationVersion = '2.3.2'
        androidxSwipeRefreshLayoutVersion= '1.1.0'

        // Material design
        materialVersion = '1.2.1'

        // Google Play Services
        playServicesAdsIdentifierVersion = '17.0.0'
        playServicesLocationVersion = '17.1.0'
        playServicesBaseVersion = '17.5.0'

        // Firebase
        firebaseFcmVersion = '21.0.0'

        // Test
        androidxTestTruthVersion = '1.2.0'
        androidxTestRunnerVersion = '1.2.0'
        androidxTestRulesVersion = '1.2.0'
        androidxTestCoreVersion = '1.3.0'
        androidxTestJunitVersion = '1.1.2'
        androidxEspressoVersion = '3.2.0'
        googleTruthVersion = '1.0.1'
        junitVersion = '4.13'
        mockitoVersion = '2.25.0'
        robolectricVersion = '4.3.1'

        // Doc Dependencies
        doclavaVersion = '1.0.6'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintrayPluginVersion"
        classpath "com.google.gms:google-services:$googleServicesPluginVersion"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:$spotlessPluginVersion"
        classpath "com.pinterest:ktlint:$ktlintPluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

configurations {
    doclava
}

dependencies {
    // Docs
    doclava("com.google.doclava:doclava:$rootProject.doclavaVersion")
}


task javadoc(type: Javadoc) {
    destinationDir = file("$buildDir/docs/javadoc")
    title = null
    failOnError false

    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    exclude '**/com/ad4screen/**'

    options {
        docletpath = configurations.doclava.files as List
        doclet "com.google.doclava.Doclava"
        bootClasspath new File(System.getenv('JAVA_HOME') + "/jre/lib/rt.jar")
        addStringOption "notimestamp", null
        addStringOption "hdf project.name", "Airship Android SDK"
        addStringOption "hdf project.version", project.version
        addStringOption "apiversion", "v2"
        addStringOption "federate android", "http://developer.android.com/reference"
        addStringOption "federationxml android", "docs/android-22.xml"
        addStringOption "templatedir", "docs/template"
    }

    subprojects.findAll { it.name.startsWith("urbanairship-") && !it.name.contains("stub") }.each { proj ->
        proj.afterEvaluate { evaluated ->
            source += evaluated.android.sourceSets.main.java.sourceFiles
            if (proj.name != 'urbanairship-fcm') {
                classpath += evaluated.files(evaluated.android.getBootClasspath().join(File.pathSeparator))

                evaluated.android.libraryVariants.all { variant ->
                    if (variant.name == 'release') {
                        classpath += variant.getJavaCompileProvider().get().classpath
                        classpath += project.files(evaluated.android.getBootClasspath().join(File.pathSeparator))
                    }
                }
            }
        }
    }
}

task packageDocs(type: Tar, dependsOn: 'javadoc') {
    into('./') {
        from 'build/docs/javadoc'
    }

    archiveFileName = "${rootProject.airshipVersion}.tar.gz"
    compression = Compression.GZIP

    destinationDir file('build/docs')
    extension 'tar.gz'
    compression = Compression.GZIP
}

task getVersion() {
    doLast {
        def version = airshipVersion
        if (project.hasProperty("airshipVersionQualifier")) {
            version += "-" + airshipVersionQualifier
        }
        println version
    }
}

apply plugin: "com.diffplug.gradle.spotless"

spotless {
    java {
        target '**/*.java'
        targetExclude '.idea/**'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    kotlin {
        target '**/*.kt'
        targetExclude '.idea/**'

        ktlint().userData(['max_line_length': 'off'])

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}
