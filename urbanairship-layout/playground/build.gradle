import com.fasterxml.jackson.databind.ObjectMapper
import com.fasterxml.jackson.dataformat.yaml.YAMLFactory

buildscript {
    ext {
        jacksonVersion='2.9.8'
        spoonPluginVersion='1.2.2'
    }

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
        // For spoon snapshots
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        classpath "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
        classpath 'com.jaredsburrows:gradle-spoon-plugin:1.5.1'

    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

apply plugin: 'com.jaredsburrows.spoon'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.urbanairship.android.layout.playground"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            assets.srcDirs += file("$buildDir/generated/sample_layout_assets/")
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

spoon {
    title = "Thomas Screenshots"
    grantAll = true
    ignoreFailures = true
}

repositories {
    // For spoon snapshots
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation project(":urbanairship-layout")

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test:core-ktx:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.squareup.spoon:spoon-client:2.0.0-SNAPSHOT'
}

task generateSampleLayouts {
    description = 'Converts yml sample layouts to json files to be merged into assets.'

    def inFiles = fileTree(dir: 'sample-layouts', include: ['*.yml', '*.yaml'])
    def outDir = file("$buildDir/generated/sample_layout_assets/sample_layouts")

    inputs.files(inFiles.toList()).withPropertyName('sourceFiles')
    outputs.dir(outDir).withPropertyName('outputDir')
    outputs.cacheIf {true }

    def yamlReader = new ObjectMapper(new YAMLFactory())
    def jsonWriter = new ObjectMapper()

    doLast {
        if (outDir.exists()) {
            outDir.delete()
        }
        outDir.mkdirs()

        inFiles.each { input ->
            def name = input.getName().replaceFirst(~/ya?ml/, "json")
            def outFile = file("$outDir/$name")
            outFile.createNewFile()

            def yaml = yamlReader.readValue(input, Object.class)
            jsonWriter.writeValue(outFile, yaml)
        }
    }
}

android.applicationVariants.all { variant ->
    variant.getMergeAssetsProvider().configure {
        dependsOn generateSampleLayouts
    }
}

