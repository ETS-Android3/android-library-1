import com.fasterxml.jackson.databind.ObjectMapper
import com.fasterxml.jackson.dataformat.yaml.YAMLFactory

buildscript {
    ext {
        jacksonVersion='2.9.8'
    }

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.urbanairship.android.layout.playground"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            assets.srcDirs += file("$buildDir/generated/sample_layout_assets/")
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(":urbanairship-layout")

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task generateSampleLayouts {
    description = 'Converts yml sample layouts to json files to be merged into assets.'

    def inFiles = fileTree(dir: 'sample-layouts', include: ['*.yml', '*.yaml'])
    def outDir = file("$buildDir/generated/sample_layout_assets/sample_layouts")

    inputs.files(inFiles.toList()).withPropertyName('sourceFiles')
    outputs.dir(outDir).withPropertyName('outputDir')
    outputs.cacheIf {true }

    def yamlReader = new ObjectMapper(new YAMLFactory())
    def jsonWriter = new ObjectMapper()

    doLast {
        outDir.mkdirs()

        inFiles.each { input ->
            def name = input.getName().replaceFirst(~/ya?ml/, "json")
            def outFile = file("$outDir/$name")
            outFile.createNewFile()

            def yaml = yamlReader.readValue(input, Object.class)
            jsonWriter.writeValue(outFile, yaml)
        }
    }
}

android.applicationVariants.all { variant ->
    variant.getMergeAssetsProvider().configure {
        dependsOn generateSampleLayouts
    }
}

